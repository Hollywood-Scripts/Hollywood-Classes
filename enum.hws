/*
** Enum.hws
**
** an Enumeration class
** by Samuel D. Crow
**
*/
Global Enum={} ; Allocate the table for the interface
Enum._v={} ; Allocate vtable for class methods

/*
** New()
**
** Initializes a new enumeration
**
** Notes:  It is recommended to have 0 as the start parameter for most occasions
**    and set it to indicate an uninitialized value has been used.
**
**    
*/
Function Enum.New(starting, ...)
	Local tbl = CopyTable(Enum._v) ; Instatiate base class
	tbl["_start"] = starting
	tbl["_end"] = starting+arg.n-1
	tbl["_data"]={}
	For Local count = starting To tbl._end Do tbl._data[arg[count-starting]] = count
	Return(tbl)
EndFunction

/*
** Dump()
**
** Outputs the contents of an enumeration class to the debug output
*/
Function Enum._v.dump(self)
	DebugPrint("_start=" .. self._start)
	DebugPrint("_end=" .. self._end)
	DebugPrint("_data={")
	For i,j In Pairs(self._data)
		DebugPrint("  " .. i .. "=" .. j)
	Next
	DebugPrint("}")
	If HaveItem(self,"_reflection")
		DebugPrint("_reflection={")
		For i,j In Pairs(self._reflection)
			DebugPrint("  ".. i .. "=" .. j)
		Next
		DebugPrint("}")
	EndIf
EndFunction

/*
** CreateReflection()
**
** Creates a "reflection" table to link values back to their keys
*/
Function Enum._v.CreateReflection(self)
	If HaveItem(self,"_reflection") Then Return
	self._reflection={}
	For i,j In Pairs(self._data)
		self._reflection[j]=i
	Next
EndFunction

/*
** Append()
**
** Add items to the current enumeration
*/
Function Enum._v.append(self, ...)
	Local ending = self._end+arg.n
	Local starting = self._end+1
	For Local count = starting To ending Do self._data[arg[count-starting]] = count
	self["_end"] = ending
	If HaveItem(self, "_reflection")
		For count = starting To ending Do self._reflection[count] = arg[count-starting]
	EndIf
EndFunction

/*
** Extend()
**
** Create child enumeration of existing one
*/
Function Enum._v.extend(self, Reflect, ...)
	Local tbl = CopyTable(self)
	If HaveItem(tbl, "_reflection") And (Reflect = False) Then tbl._reflection = Nil
	tbl:append(Unpack(arg))
	Return(tbl)
EndFunction

